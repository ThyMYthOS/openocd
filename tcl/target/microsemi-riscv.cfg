#**************************************************************************
#   Copyright (C) 2015-2018 by Microchip Technology Inc.                  *
#   http://www.microchip.com/support                                      *
#                                                                         *
#   This program is free software; you can redistribute it and/or modify  *
#   it under the terms of the GNU General Public License as published by  *
#   the Free Software Foundation; either version 2 of the License, or     *
#   (at your option) any later version.                                   *
#                                                                         *
#   This program is distributed in the hope that it will be useful,       *
#   but WITHOUT ANY WARRANTY; without even the implied warranty of        *
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the         *
#   GNU General Public License for more details.                          *
#                                                                         *
#   You should have received a copy of the GNU General Public License     *
#   along with this program.  If not, see <http://www.gnu.org/licenses/>. *
#**************************************************************************

#------------------------------------------------------------------------------
# Microsemi RISC-V
#
# https://www.microsemi.com/product-directory/embedded-processing/4406-cpus
#------------------------------------------------------------------------------

# Device name to device family name lookup table
array set device_family {
    AFS090          Fusion
    AFS250          Fusion
    M1AFS250        Fusion
    U1AFS250        Fusion
    AFS600          Fusion
    M1AFS600        Fusion
    P1AFS600        Fusion
    U1AFS600        Fusion
    AFS1500         Fusion
    M1AFS1500       Fusion
    P1AFS1500       Fusion
    U1AFS1500       Fusion
    M2GL005         IGLOO2
    M2GL010         IGLOO2
    M2GL025         IGLOO2
    M2GL050         IGLOO2
    M2GL060         IGLOO2
    M2GL090         IGLOO2
    M2GL150         IGLOO2    
    A2F060          SmartFusion
    A2F200          SmartFusion
    A2F500          SmartFusion
    M2S005          SmartFusion2
    M2S010          SmartFusion2
    M2S025          SmartFusion2
    M2S050          SmartFusion2
    M2S060          SmartFusion2
    M2S090          SmartFusion2
    M2S150          SmartFusion2
    A4P200          SmartFusion2
}

# Device name to device eNVM size lookup table
array set device_envm_size {
    AFS090          0x40000
    AFS250          0x40000
    M1AFS250        0x40000
    U1AFS250        0x40000
    AFS600          0x80000
    M1AFS600        0x80000
    P1AFS600        0x80000
    U1AFS600        0x80000
    AFS1500         0x100000
    M1AFS1500       0x100000
    P1AFS1500       0x100000
    U1AFS1500       0x100000
    M2GL005         0x20000
    M2GL010         0x40000
    M2GL025         0x40000
    M2GL050         0x40000
    M2GL060         0x40000
    M2GL090         0x80000
    M2GL150         0x80000
    A2F060          0x20000
    A2F200          0x40000
    A2F500          0x80000
    M2S005          0x20000
    M2S010          0x40000
    M2S025          0x40000
    M2S050          0x40000
    M2S060          0x40000
    M2S090          0x80000
    M2S150          0x80000
    A4P200          0x20000
}

# Device family name to device eNVM driver name lookup table
# Note that microsemi-smartfusion2-envm works for SmartFusion2 and IGLOO2
array set envm_driver {
    Fusion          microsemi-fusion-coreahbnvm
    IGLOO2          microsemi-smartfusion2-envm
    SmartFusion     microsemi-smartfusion-envm
    SmartFusion2    microsemi-smartfusion2-envm
}

# Device family name to device eNVM default base address lookup table
# Fusion envm   base address arbitrarily defaulted to 0x00000000
array set envm_base {
    Fusion          0x00000000
    IGLOO2          0x60000000
    SmartFusion     0x60000000
    SmartFusion2    0x60000000
}

# 
# Process DEVICE variable
#

if {![exists DEVICE]} {
    # Not set so default to the generic "FPGA"
    set DEVICE "FPGA"
} else {
    # Normalise - convert to uppercase
    set DEVICE [string toupper $DEVICE]
	
    # Look up DEVICE
    if {[exists device_family($DEVICE)]} {
        # if found then default flash parameters unless already set
        if {![exists FLASH_DRIVER]} {
            set FLASH_DRIVER $envm_driver($device_family($DEVICE)) 
        }
        if {![exists FLASH_BASE]} {
            set FLASH_BASE $envm_base($device_family($DEVICE)) 
        }
        if {![exists FLASH_SIZE]} {
            set FLASH_SIZE $device_envm_size($DEVICE)
        }
    }
}

# G5SOC has a single TAP for FPGA and U54-MC Coreplex
if {$DEVICE eq "G5SOC"} {
    set FPGA_TAP "N"
}

# IRCODE needed to address UJTAG/uj_jtag slave 
if {![exists UJ_JTAG_IRCODE]} {
    # TODO: validate - e.g. 2 hex digit value
    set UJ_JTAG_IRCODE 0x55
}

# COREID - assume coreid 0 unless told otherwise
if {![exists COREID]} {
    set COREID 0
}

if {$FPGA_TAP eq "Y"} {

    # FPGA TAP present, occludes RISC-V CPU DTM TAP and must be disabled/bypassed first

    # Start off in standard JTAG mode
    microsemi_flashpro tunnel_jtag_via_ujtag off	

    #
    # FPGA TAP
    #

    jtag newtap $DEVICE tap -irlen 8 -expected-id 0 -ignore-version
    jtag configure $DEVICE.tap -event tap-disable "disable_fpga_tap $DEVICE.tap"
    jtag configure $DEVICE.tap -event setup "switch_from_fpga_tap_to_riscv_tap $DEVICE.tap $DEVICE.cpu"

    #
    # RISC-V CPU DTM TAP - disabled/not visible by default until FPGA TAP disabled/bypassed
    #

    jtag newtap $DEVICE cpu -irlen 5 -expected-id 0x10e31913 -disable
    jtag configure $DEVICE.cpu -event tap-enable "enable_riscv_tap"
	
    #
    # TAP management event handlers/procedures
    #

    # Switch from FPGA TAP to RISC-V CPU DTM TAP (by tunnelling JTAG via UJTAG)
    proc switch_from_fpga_tap_to_riscv_tap {fpga_tap riscv_tap} {
        jtag tapdisable $fpga_tap
        jtag tapenable $riscv_tap

        # Read RISC-V CPU DTM IDCODE as a sanity check
        irscan $riscv_tap 0x01
        set idcode [string tolower [drscan $riscv_tap 32 0]]
        echo [format "Info : RISC-V IDCODE = 0x%s" $idcode]
    }

    # Disable/bypass FPGA TAP
    proc disable_fpga_tap {fpga_tap} {
        global UJ_JTAG_IRCODE
		
        # Tell UJTAG/uj_jtag to address RISC-V CPU DTM
        irscan $fpga_tap $UJ_JTAG_IRCODE -endstate IRPAUSE
        runtest 8
		
        # Enable tunnelled JTAG mode
        microsemi_flashpro tunnel_jtag_via_ujtag on
    }

    # Enable RISC-V CPU DTM TAP
    proc enable_riscv_tap {} {
        # Nothing to do here - the work is done in disable_fpga_tap
        # but the handler still needs to be defined
    }	
} else {
    # "Direct" debugging not via FPGA TAP
    
    # Select appropriate irlen and expected-id
    if {$DEVICE eq "G5SOC"} {
        set IRLEN 8
        set EXPECTED_ID 0
    } else {
        set IRLEN 5
        set EXPECTED_ID 0x10e31913
    }
    	
    #
    # RISC-V CPU DTM TAP 
    #

    jtag newtap $DEVICE cpu -irlen $IRLEN -expected-id $EXPECTED_ID
}

#
# Target CPU
#

set _TARGETNAME $DEVICE.dtm

# Debug connection to specific coreid or one to all cores/harts if COREID == -1
if {$COREID eq "-1"} {
    target create $_TARGETNAME riscv -chain-position $DEVICE.cpu -rtos riscv
} else {
    target create $_TARGETNAME riscv -chain-position $DEVICE.cpu -coreid $COREID
}

#
# Flash
#

#
# Flash may be defined via parameters:
# FLASH_DRIVER: cfi or microsemi-fusion-coreahbnvm (for Fusion/AFSXXX) or
#     microsemi-smartfusion2-envm (for SmartFusion2/M2SXXX) or
#     microsemi-smartfusion-envm (for SmartFusion/A2FXXX)
# FLASH_BASE - e.g. 0x10000000
# FLASH_SIZE - e.g. 0x8000
# FLASH_WIDTH - e.g. 0, 1, 2 or 4 or not defined (defaults to 0)
# If FLASH_DRIVER is defined then the other parameters are assumed to be defined
# and to be valid
#

if {[exists FLASH_DRIVER]} {
    flash bank $DEVICE.flash $FLASH_DRIVER $FLASH_BASE $FLASH_SIZE 0 0 $_TARGETNAME
}

#
# Reset configuration
#

# Only TRSTn supported
reset_config trst_only

#
# Event handlers
#

# reset-init event handler
$_TARGETNAME configure -event reset-init {
    # call board level reset-init if defined
    if {[exists -proc do_board_reset_init]} {
        do_board_reset_init
    }
}

# examine-end event handler
$_TARGETNAME configure -event examine-end {
    # TODO: halt should not be needed/used if reset run|halt|init worked but
    # our RV32IM implementation does not fully implement the Debug Control &
    # Status register (csr #0x7b0 aka csr #1968 aka csr1968 aka dcsr) - in 
    # particular ndreset and fullreset bits.
    halt
}

# gdb-detach event handler
$_TARGETNAME configure -event gdb-detach {
    # resume execution on debugger detach
    resume
}

# TODO: why does this eliminate the second (redundant?) probe/examine?
proc init_reset {mode} {
}

